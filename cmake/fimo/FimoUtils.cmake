include(CMakeDependentOption)

macro(new_fimo_module NAME DEFAULT)
    if (DEFINED FIMO_MODULE_${NAME})
        message(FATAL_ERROR "Module ${NAME} was already declared")
    endif ()

    set(FIMO_MODULE_${NAME} "")
    cmake_dependent_option(ENABLE_BINDINGS_${NAME} "Enables the ${NAME} bindings" ${DEFAULT} "NOT DISABLE_BINDINGS" OFF)
    cmake_dependent_option(ENABLE_MODULE_${NAME} "Enables the ${NAME} module" ${DEFAULT} "NOT DISABLE_MODULES" OFF)
endmacro()

macro(fimo_bindings_enabled NAME VAR)
    if (NOT DEFINED FIMO_MODULE_${NAME})
        message(FATAL_ERROR "No module with the name ${NAME} was declared")
    endif ()
    if (ENABLE_ALL_BINDINGS OR ENABLE_BINDINGS_${NAME})
        set(${VAR} TRUE)
    else ()
        set(${VAR} FALSE)
    endif ()
endmacro()

macro(fimo_module_enabled NAME VAR)
    if (NOT DEFINED FIMO_MODULE_${NAME})
        message(FATAL_ERROR "No module with the name ${NAME} was declared")
    endif ()
    if (ENABLE_ALL_MODULES OR ENABLE_MODULE_${NAME})
        message(STATUS "${ENABLE_ALL_MODULES} ${ENABLE_MODULE_${NAME}}")
        set(${VAR} TRUE)
    else ()
        set(${VAR} FALSE)
    endif ()
endmacro()

function(fimo_require_bindings NAME)
    fimo_bindings_enabled(${NAME} ENABLED)
    if (NOT ENABLED)
        message(FATAL_ERROR "Bindings for module ${NAME} are not enabled.")
    endif ()
endfunction()

function(fimo_require_module NAME)
    fimo_module_enabled(${NAME} ENABLED)
    if (NOT ENABLED)
        message(FATAL_ERROR "Module ${NAME} is not enabled.")
    endif ()
endfunction()

function(fimo_include_bindings NAME PATH)
    if (NOT DEFINED FIMO_MODULE_${NAME})
        message(FATAL_ERROR "No module with the name ${NAME} was declared")
    endif ()

    fimo_bindings_enabled(${NAME} ENABLED)
    if (ENABLED)
        message(STATUS "Building bindings: ${NAME}")
        add_subdirectory(${PATH})
    endif ()
endfunction()

function(fimo_include_module NAME PATH)
    if (NOT DEFINED FIMO_MODULE_${NAME})
        message(FATAL_ERROR "No module with the name ${NAME} was declared")
    endif ()

    fimo_module_enabled(${NAME} ENABLED)
    if (ENABLED)
        message(STATUS "Building module: ${NAME}")
        add_subdirectory(${PATH})
    endif ()
endfunction()